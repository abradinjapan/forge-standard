[
    Buffer
]

[
    List
]
[ list type data ]
forge.list.metadata()(byte_size) = {
    [ set data ]
    forge.set(forge.integer.4)(cell_count)
    forge.integer.multiply(forge.constant.cell_byte_size cell_count)(byte_size)
}

[ set list ]
forge.list.set(list.start list.end list.buffer.start list.buffer.end list.filled_index list.increase)() = {
    [ set data ]
    forge.copy(list.start)(address)
    forge.io.cell_to_address(#forge.always list.buffer.start forge.constant.cell_bit_size)(address)
    forge.integer.add(address forge.constant.cell_byte_size)(address)
    forge.io.cell_to_address(#forge.always list.buffer.end forge.constant.cell_bit_size)(address)
    forge.integer.add(address forge.constant.cell_byte_size)(address)
    forge.io.cell_to_address(#forge.always list.filled_index forge.constant.cell_bit_size)(address)
    forge.integer.add(address forge.constant.cell_byte_size)(address)
    forge.io.cell_to_address(#forge.always list.increase forge.constant.cell_bit_size)(address)
}

[ get list ]
forge.list.get(list.start list.end)(list.buffer.start list.buffer.end list.filled_index list.increase) = {
    [ get data ]
    forge.copy(list.start)(address)
    forge.io.address_to_cell(#forge.always address forge.constant.cell_bit_size)(list.buffer.start)
    forge.integer.add(address forge.constant.cell_byte_size)(address)
    forge.io.address_to_cell(#forge.always address forge.constant.cell_bit_size)(list.buffer.end)
    forge.integer.add(address forge.constant.cell_byte_size)(address)
    forge.io.address_to_cell(#forge.always address forge.constant.cell_bit_size)(list.filled_index)
    forge.integer.add(address forge.constant.cell_byte_size)(address)
    forge.io.address_to_cell(#forge.always address forge.constant.cell_bit_size)(list.increase)
}

[ open a list ]
forge.list.open(increase)(list.start list.end) = {
    [ get list metadata ]
    forge.list.metadata()(byte_size)

    [ allocate list container ]
    forge.memory.request(byte_size)(list.start list.end)

    [ allocate data buffer ]
    forge.memory.request(increase)(list.buffer.start list.buffer.end)

    [ set data in list ]
    forge.list.set(list.start list.end list.buffer.start list.buffer.end forge.constant.0 increase)()
}

[ close a list ]
forge.list.close(list.start list.end)() = {
    [ retrieve data ]
    forge.list.get(list.start list.end)(list.buffer.start list.buffer.end list.filled_index list.increase)

    [ close buffers ]
    forge.memory.return(list.buffer.start list.buffer.end)()
    forge.memory.return(list.start list.end)()
}

[ check for space and expand list if necessary ]
forge.list.request_space(list.start list.end space_needed)() = {
    [ get list data ]
    forge.list.get(list.start list.end)(list.buffer.start list.buffer.end list.filled_index list.increase)

    [ check if space is required ]
    forge.integer.add(list.filled_index space_needed)(new_filled_index)
    forge.buffer.calculate_length(list.buffer.start list.buffer.end)(list.buffer_length)
    forge.integer.subtract(list.buffer_length forge.constant.1)(list.buffer_max)
    forge.integer.within_range(forge.constant.0 new_filled_index list.buffer_max #forge.never)(#doesnt_need_more_space)

    [ jump past space allocator ]
    forge.jump(#doesnt_need_more_space @skip_reallocation)()

    [ calculate new list size from increase ]
    forge.integer.divide(new_filled_index list.increase)(increase_units_long)
    forge.integer.add(increase_units_long forge.constant.1)(increase_units_long)
    forge.integer.multiply(increase_units_long list.increase)(new_allocation_size)

    [ allocate new buffer ]
    forge.memory.request(new_allocation_size)(list.new_buffer.start list.new_buffer.end)

    [ calculate the end of the old buffer but for the new buffer ]
    forge.integer.add(list.new_buffer.start list.filled_index)(new_buffer.old_end)

    [ setup list buffer currents ]
    forge.integer.add(list.buffer.start list.filled_index)(list.buffer.current.end)

    [ copy old buffer to new buffer ]
    forge.copy.buffer(list.buffer.start list.buffer.current.end)(list.new_buffer.start new_buffer.old_end)

    [ deallocate old buffer ]
    forge.memory.return(list.buffer.start list.buffer.end)()

    [ set new buffer to list ]
    forge.copy(list.new_buffer.start)(list.buffer.start)
    forge.copy(list.new_buffer.end)(list.buffer.end)

    [ skip reallocation ]
    @skip_reallocation

    [ update list ]
    forge.list.set(list.start list.end list.buffer.start list.buffer.end list.filled_index list.increase)()
}

[ append data to list ]
forge.list.append.buffer_contents(list.start list.end buffer.start buffer.end)() = {
    [ calculate buffer size ]
    forge.buffer.calculate_length(buffer.start buffer.end)(buffer.length)

    [ expand list if necessary ]
    forge.list.request_space(list.start list.end buffer.length)()

    [ calculate buffer copy addresses ]
    forge.list.calculate.filled_address(list.start list.end)(list.filled.start list.filled.end)
    forge.integer.add(list.filled.end forge.constant.1)(list.new_data.start)
    forge.integer.add(list.new_data.start buffer.length)(list.new_data.end)
    forge.integer.subtract(list.new_data.end forge.constant.1)(list.new_data.end)

    [ copy data ]
    forge.copy.buffer(buffer.start buffer.end)(list.new_data.start list.new_data.end)

    [ get list ]
    forge.list.get(list.start list.end)(list.buffer.start list.buffer.end list.filled_index list.increase)

    [ update list filled index ]
    forge.integer.add(list.filled_index buffer.length)(list.new_filled_index)

    [ update list ]
    forge.list.set(list.start list.end list.buffer.start list.buffer.end list.new_filled_index list.increase)()
}

[ append cell to list ]
forge.list.append.cell(list.start list.end cell cell.bit_count)() = {
    [ convert bit count to byte count ]
    forge.integer.divide(cell.bit_count forge.constant.bits_in_byte)(bytes)

    [ expand list if necessary ]
    forge.list.request_space(list.start list.end bytes)()

    [ calculate cell copy address ]
    forge.list.calculate.filled_address(list.start list.end)(list.filled.start list.filled.end)
    forge.integer.add(list.filled.end forge.constant.1)(list.new_data.start)

    [ copy data ]
    forge.io.cell_to_address(#forge.always cell cell.bit_count)(list.new_data.start)

    [ get list ]
    forge.list.get(list.start list.end)(list.buffer.start list.buffer.end list.filled_index list.increase)

    [ update list filled index ]
    forge.integer.add(list.filled_index bytes)(list.new_filled_index)

    [ update list ]
    forge.list.set(list.start list.end list.buffer.start list.buffer.end list.new_filled_index list.increase)()
}

[ append a buffer to a list ]
forge.list.append.buffer_addresses(list.start list.end buffer.start buffer.end)() = {
    [ append both addresses ]
    forge.list.append.cell(list.start list.end buffer.start forge.constant.cell_bit_size)()
    forge.list.append.cell(list.start list.end buffer.end forge.constant.cell_bit_size)()
}

[ calculate list end address ]
forge.list.calculate.filled_address(list.start list.end)(list.filled_buffer.start list.filled_buffer.end) = {
    [ get list data ]
    forge.list.get(list.start list.end)(list.buffer.start list.buffer.end list.filled_index list.increase)

    [ set list filled buffer start ]
    forge.copy(list.buffer.start)(list.filled_buffer.start)

    [ calculate end address ]
    forge.integer.add(list.buffer.start list.filled_index)(list.filled_buffer.end)
    forge.integer.subtract(list.filled_buffer.end forge.constant.1)(list.filled_buffer.end) [ adjusts list end address to proper place ]
}

[
    Printing
]
[ print new line ]
forge.print.new_line()() = {
    forge.set(forge.integer.10)(new_line)
    forge.print.character(new_line)()
}

[ print space ]
forge.print.space()() = {
    forge.set(forge.integer.32)(space)
    forge.print.character(space)()
}

[
    Larsing
]
[ generate larser from larse blueprint ]
forge.larser.generate(blueprint.start blueprint.end)(larser.start larser.end) = {
    [ todo ]
    forge.copy(forge.constant.0)(larser.start)
    forge.copy(forge.constant.0)(larser.end)
}

[ open larser blueprint ]
forge.larser_blueprint.open()(blueprint_list.start blueprint_list.end) = {
    [ setup variables ]
    forge.set(forge.integer.2048)(increase)
    forge.integer.multiply(increase forge.constant.cell_byte_size)(increase)

    [ open list ]
    forge.list.open(increase)(blueprint_list.start blueprint_list.end)
}

[ print larser blueprint ]
forge.larser_blueprint.print(blueprint_list.start blueprint_list.end)() = {
    [ create strings ]
    forge.set("[ ")(right.start right.end)
    forge.set(" ]")(left.start left.end)

    [ create relevant addresses ]
    forge.list.calculate.filled_address(blueprint_list.start blueprint_list.end)(blueprint.start blueprint.end)
    forge.copy(blueprint.start)(blueprint.current)

    [ print each blueprintling ]
    [ setup loop start offset ]
    @loop_start

    [ check current ]
    forge.integer.within_range(blueprint.start blueprint.current blueprint.end #forge.always)(#exit_loop)
    forge.jump(#exit_loop @loop_end)()

    [ retrieve data ]
    forge.io.address_to_cell(#forge.always blueprint.current forge.constant.cell_bit_size)(data)

    [ print data ]
    forge.print.buffer_as_string(right.start right.end)()
    forge.print.integer.unsigned(data)()
    forge.print.buffer_as_string(left.start left.end)()
    forge.print.new_line()()

    [ advance current ]
    forge.integer.add(blueprint.current forge.constant.cell_byte_size)(blueprint.current)

    [ jump to beginning ]
    forge.jump(#forge.always @loop_start)()

    [ create loop end ]
    @loop_end
}
